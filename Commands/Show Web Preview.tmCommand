<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE plist PUBLIC "-//Apple//DTD PLIST 1.0//EN" "http://www.apple.com/DTDs/PropertyList-1.0.dtd">
<plist version="1.0">
<dict>
	<key>beforeRunningCommand</key>
	<string>nop</string>
	<key>command</key>
	<string>#!/usr/bin/env osascript

-- `menu_click`, by Jacob Rus, September 2006
-- 
-- Accepts a list of form: `{"Finder", "View", "Arrange By", "Date"}`
-- Execute the specified menu item.  In this case, assuming the Finder 
-- is the active application, arranging the frontmost folder by date.

on menu_click(mList)
	local appName, topMenu, r
	
	-- Validate our input
	if mList's length &lt; 3 then error "Menu list is not long enough"
	
	-- Set these variables for clarity and brevity later on
	set {appName, topMenu} to (items 1 through 2 of mList)
	set r to (items 3 through (mList's length) of mList)
	
	-- This overly-long line calls the menu_recurse function with
	-- two arguments: r, and a reference to the top-level menu
	tell app "System Events" to my menu_click_recurse(r, ((process appName)'s (menu bar 1)'s (menu bar item topMenu)'s (menu topMenu)))
end menu_click

on menu_click_recurse(mList, parentObject)
	local f, r
	
	-- `f` = first item, `r` = rest of items
	set f to item 1 of mList
	if mList's length &gt; 1 then set r to (items 2 through (mList's length) of mList)
	
	-- either actually click the menu item, or recurse again
	tell app "System Events"
		if mList's length is 1 then
			click parentObject's menu item f
		else
			my menu_click_recurse(r, (parentObject's (menu item f)'s (menu f)))
		end if
	end tell
end menu_click_recurse

on UIscript_check()
	-- check to see if assistive devices is enabled
	tell application "System Events" to set UI_enabled to UI elements enabled
	if not UI_enabled then
		tell application "AppleScript Utility"
			activate
			display dialog "This script utilizes the built-in Graphical User Interface Scripting architecture of Mac OS X which is currently disabled." &amp; return &amp; return &amp; "You can activate GUI Scripting by selecting the checkbox “Enable GUI Scripting” in AppleScript Utility." with icon 1 buttons {"Okay"} default button 1
		end tell
	end if
end UIscript_check

UIscript_check()

menu_click({"TextMate", "Window", "Show Web Preview"})
</string>
	<key>input</key>
	<string>none</string>
	<key>keyEquivalent</key>
	<string>^~@p</string>
	<key>name</key>
	<string>Show Web Preview</string>
	<key>output</key>
	<string>discard</string>
	<key>scope</key>
	<string>text.html.textile, text.html.markdown, source, source.css, source.js</string>
	<key>uuid</key>
	<string>C9CA5346-CE6D-4162-92ED-91D1B8F94E27</string>
</dict>
</plist>
